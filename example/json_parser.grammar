%{
import 'package:source_span/source_span.dart';

Object? parse(String source) {
  const parser = JsonParser();
  final state = State(source);
  final result = parser.parseStart(state);
  if (result == null) {
    final file = SourceFile.fromString(source);
    final message = state
        .getErrors()
        .map((e) => file.span(e.start, e.end).message(e.message))
        .join('\n');
    throw FormatException('\n$message');
  }

  return result.$1;
}
}%

%%
const JsonParser();
%%

`Object?` Start =>
  S
  $ = Value
  & { state.ch < 0 }
  ~{ state.errorExpected('enf of file'); }

`List<Object?>` Elements =>
  v = Value
  { final l = [v]; }
  @while (0) {
    ',' S
    v = Value
    { l.add(v); }
  }
  $ = { l }

`List<Object?>` Array =>
  "[" S
  e = Elements?
  ']' S
  $ = { e ?? [] }

`MapEntry<String, Object?>` KeyValue =>
  k = String
  ~{ state.errorExpected('string'); }
  ':' S
  v = Value
  $ = { MapEntry(k, v) }

`Map<String, Object?>` Map =>
  v = KeyValue
  {
    final m = <String, Object?>{};
    m[v.key] = v.value;
  }
  @while (0) {
    ',' S
    v = KeyValue
    { m[v.key] = v.value; }
  }
  $ = { m }

`Map<String, Object?>` Object =>
  "{" S
  m = Map?
  '}' S
  $ = { m ?? {} }

`String` EscapeC =>
  (
    ["]
    $ = `const` { '"' }
    ----
    [\\]
    $ = `const` { '\\' }
    ----
    [/]
    $ = `const` { '/' }
    ----
    [b]
    $ = `const` { '\b' }
    ----
    [f]
    $ = `const` { '\f' }
    ----
    [n]
    $ = `const` { '\n' }
    ----
    [r]
    $ = `const` { '\r' }
    ----
    [t]
    $ = `const` { '\t' }
  )
  ~{
    if (state.position == state.length) {
      state.errorExpected('escape character');
    } else {
      state.error('Illegal escape character');
    }
  }

`String` EscapeUnicode =>
  { final start = state.position; }
  "u"
  { var end = 0; }
  s = <
    @while (4, 4) {
      [a-fA-F0-9]
      ~{
        end = state.position;
        state.errorExpected('hexadecimal digit');
      }
    }
  >
  ~{ state.error('Incorrect Unicode escape sequence', position: end, start: start, end: end); }
  $ = { String.fromCharCode(int.parse(s, radix: 16)) }

`String` Escaped =>
  & "u"
  $ = EscapeUnicode
  ----
  EscapeC

`String` String =>
  { final start = state.position; }
  ["]
  p = @while (0) {
    <[^{0-1F}"\\]+>
    ---
    [\\]
    $ = Escaped
  }
  ["]
  ~{
    state.error('Unterminated string', start: start);
    state.errorExpected('"');
  }
  S
  $ = { p.length == 1 ? p[0] : p.isNotEmpty ? p.join() : '' }

`num` Number =>
  {
    final start = state.position;
    var flag = true;
  }
  [-]?
  ([0] / [1-9] [0-9]*)
  ~{ state.errorExpected('digit'); }
  (
    [.]
    [0-9]+
    ~{
      state.errorExpected('digit');
      state.error('Fractional part is missing a number');
      state.error('Malformed number', start: start, end: state.position);
    }
    { flag = false; }
  )?
  (
    [eE]
    [\-+]?
    [0-9]+
    ~{
      state.errorExpected('digit');
      state.error('Exponent part is missing a number');
      state.error('Malformed number', start: start, end: state.position);
    }
    { flag = false; }
  )?
  s = { state.substring(start, state.position) }
  S
  $ = { flag && s.length <= 18 ? int.parse(s) : num.parse(s) }

`Object?` Value =>
  (
    "null" S
    $ = `const` { null }
    ----
    "true" S
    $ = `const` { true }
    ----
    "false" S
    $ = `const` { false }
    ----
    & "{"
    $ = Object
    ----
    & "["
    $ = Array
    ----
    & ["]
    $ = String
    ----
    Number
  )
  ~{ state.errorExpected(const ['string', 'number', 'array', 'object', 'null', 'boolean value']); }

  `void` S =>
    [\n\r\t ]*